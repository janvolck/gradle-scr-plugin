apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'be.jlr-home.gradle'
version = '0.1.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.apache.felix:org.apache.felix.scr.generator:1.8.+'
    compile 'org.apache.felix:org.apache.felix.scr.ds-annotations:1.2.+'
    compile 'org.osgi:org.osgi.compendium:5.0.0'
    compile 'org.codehaus.plexus:plexus-utils:3.0.15'
    testCompile 'junit:junit:4.+'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gradle SCR Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A wrapper around the felix scr generation so it can be easily used in gradle to create scr metadata for OSGi Bundles.'
                url 'https://github.com/janvolck/gradle-scr-plugin'

                scm {
                    connection 'scm:git:git@github.com:janvolck/gradle-scr-plugin.git'
                    developerConnection 'scm:git:git@github.com:janvolck/gradle-scr-plugin.git'
                    url 'git@github.com:janvolck/gradle-scr-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'janvolck'
                        name 'Jan Volckaert'
                        email 'janvolck@jlr-home.be'
                    }
                }
            }
        }
    }
}
